import oc::raylib::*
import oc::rcamera::*

const MAX_COLUMNS: i32 = 20

const screen_width: i32 = 800
const screen_height: i32 = 450

def main() {
    //init
    SetExitKey(KEY_ESCAPE)
    SetTraceLogLevel(LOG_WARNING);
    InitWindow(screen_width, screen_height, "3d_camera_first_person")
    defer CloseWindow()
    SetTargetFPS(60)

    DisableCursor()
    let camera: Camera
    camera.position = Vector3(0.0, 2.0, 4.0)
    camera.target = Vector3(0.0, 2.0, 0.0)
    camera.up = Vector3(0.0, 1.0, 0.0)
    camera.fovy = 60.0
    camera.projection = CAMERA_PERSPECTIVE
    let camera_mode = CAMERA_FIRST_PERSON

    let heights: [f32; MAX_COLUMNS]
    let positions: [Vector3; MAX_COLUMNS]
    let colors: [Color; MAX_COLUMNS]
    for let i:i32=0; i < MAX_COLUMNS; ++i {
        heights[i] = GetRandomValue(1, 12) as f32
        positions[i] = Vector3(GetRandomValue(-15, 15) as f32, heights[i]/2.0, GetRandomValue(-15, 15) as f32)
        colors[i] = Color(GetRandomValue(20, 255) as u8, GetRandomValue(10, 55) as u8, 30, 255)
    }
    while(not WindowShouldClose()) {
        let key = GetKeyPressed()
        match key {
            KEY_ONE => {
                camera_mode = CAMERA_FREE
                camera.up = Vector3(0.0, 1.0, 0.0)
            }
            KEY_TWO => {
                camera_mode = CAMERA_FIRST_PERSON
                camera.up = Vector3(0.0, 1.0, 0.0)
            }
            KEY_THREE => {
                camera_mode = CAMERA_THIRD_PERSON
                camera.up = Vector3(0.0, 1.0, 0.0)
            }
            KEY_FOUR => {
                camera_mode = CAMERA_ORBITAL
                camera.up = Vector3(0.0, 1.0, 0.0)
            }
            KEY_F => {
                match camera.projection {
                    CAMERA_PERSPECTIVE => {
                        camera_mode = CAMERA_THIRD_PERSON
                        camera.position = Vector3(0.0, 2.0, -100.0)
                        camera.target = Vector3(0.0, 2.0, 0.0)
                        camera.up = Vector3(0.0, 1.0, 0.0)
                        camera.projection = CAMERA_ORTHOGRAPHIC
                        camera.fovy = 20.0
                        CameraYaw(&camera, -135.0 * DEG2RAD, true)
                        CameraPitch(&camera, -45.0 * DEG2RAD, true, true, false)
                    }
                    CAMERA_ORTHOGRAPHIC => {
                        camera_mode = CAMERA_THIRD_PERSON
                        camera.position = Vector3(0.0, 2.0, 10.0)
                        camera.target = Vector3(0.0, 2.0, 0.0)
                        camera.up = Vector3(0.0, 1.0, 0.0)
                        camera.projection = CAMERA_PERSPECTIVE
                        camera.fovy = 60.0
                    }
                    else => {}
                }
            }
            else => {}
        }

        UpdateCamera(&camera, camera_mode);

        //draw --
        BeginDrawing()
            ClearBackground(DARKGRAY)

            BeginMode3D(camera)
                DrawPlane(Vector3(0.0, 0.0, 0.0), Vector2(32.0, 32.0), LIGHTGRAY); // Draw ground
                DrawCube(Vector3(-16.0, 2.5, 0.0), 1.0, 5.0, 32.0, BLUE);     // Draw a blue wall
                DrawCube(Vector3(16.0, 2.5, 0.0), 1.0, 5.0, 32.0, LIME);      // Draw a green wall
                DrawCube(Vector3(0.0, 2.5, 16.0), 32.0, 5.0, 1.0, GOLD);      // Draw a yellow wall
                for let i:i32 = 0; i < MAX_COLUMNS; ++i {
                    DrawCube(positions[i], 2.0, heights[i], 2.0, colors[i])
                    DrawCubeWires(positions[i], 2.0, heights[i], 2.0, MAROON)
                }
                if camera_mode == CAMERA_THIRD_PERSON {
                    DrawCube(camera.target, 0.5, 0.5, 0.5, PURPLE);
                    DrawCubeWires(camera.target, 0.5, 0.5, 0.5, DARKPURPLE);
                }
            EndMode3D();

            DrawRectangle(5, 5, 330, 100, Fade(SKYBLUE, 0.5))
            DrawRectangleLines(5, 5, 330, 100, BLUE)

            DrawText("Camera controls:", 15, 15, 10, BLACK)
            DrawText("- Move keys: W, A, S, D, Space, Left-Ctrl", 15, 30, 10, BLACK)
            DrawText("- Look around: arrow keys or mouse", 15, 45, 10, BLACK)
            DrawText("- Camera mode keys: 1, 2, 3, 4", 15, 60, 10, BLACK)
            DrawText("- Zoom keys: num-plus, num-minus or mouse scroll", 15, 75, 10, BLACK)
            DrawText("- Camera projection key: F", 15, 90, 10, BLACK)

            DrawRectangle(600, 5, 195, 100, Fade(SKYBLUE, 0.5))
            DrawRectangleLines(600, 5, 195, 100, BLUE)

            DrawText("Camera status:", 610, 15, 10, BLACK)
            DrawText(TextFormat("- Mode: %s", match camera_mode {
                CAMERA_FREE => "FREE"
                CAMERA_FIRST_PERSON => "FIRST_PERSON"
                CAMERA_THIRD_PERSON => "THIRD_PERSON"
                CAMERA_ORBITAL => "ORBITAL"
                else => "CUSTOM"
            }), 610, 30, 10, BLACK)

            DrawText(TextFormat("- Projection: %s", match camera.projection {
                CAMERA_PERSPECTIVE => "PERSPECTIVE"
                CAMERA_ORTHOGRAPHIC => "ORTHOGRAPHIC"
                else => "CUSTOM"
            }), 610, 45, 10, BLACK);

            DrawText(TextFormat("- Position: (%06.3f, %06.3f, %06.3f)", camera.position.x, camera.position.y, camera.position.z), 610, 60, 10, BLACK);
            DrawText(TextFormat("- Target: (%06.3f, %06.3f, %06.3f)", camera.target.x, camera.target.y, camera.target.z), 610, 75, 10, BLACK);
            DrawText(TextFormat("- Up: (%06.3f, %06.3f, %06.3f)", camera.up.x, camera.up.y, camera.up.z), 610, 90, 10, BLACK);

        EndDrawing()
    }
    //uninit
}


